// JSONScript.mel
// Author: Sean Griffin
// Modified by: Matt DesLauriers
// Email: sean@thoughtbot.com

global proc int SimpleJSONScript(string $parent, string $action, string $settings, string $callback)
{
    if ($action == "post")
    {
        setParent $parent;
        columnLayout -adj true;

          frameLayout -cll true -cl false -bv true -l "General Export Options";
            columnLayout -adj true;
              checkBox -v true -l "Vertices" vertsCb;
              checkBox -v true -l "Normals" normalsCb;
              checkBox -v true -l "UVs" uvsCb;
              checkBox -v true -l "Groups" groupsCb;
              checkBox -v false -l "Dedupe" dedupeCb;
              setParent ..; // columnLayout
            setParent ..; // frameLayout

          frameLayout -cll true -cl false -bv true -l "Skinning Options";
            columnLayout -adj true;
              checkBox -v true -l "Materials" materialsCb;
              checkBox -v true -l "Diffuse Maps" diffuseMapsCb;
              checkBox -v true -l "Specular Maps" specularMapsCb;
              checkBox -v true -l "Bump Maps" bumpMapsCb;
              setParent ..; // columnLayout
            setParent ..; // frameLayout

          frameLayout -cll true -cl false -bv true -l "Precision";
            columnLayout -adj true;
              intSliderGrp -label "Precision"
                           -field true
                           -maxValue 16
                           -minValue 1
                           -fieldMinValue 1
                           -fieldMaxValue 16
                           -value 3
                           precisionCb;
            setParent ..; // columnLayout
          setParent ..; // frameLayout

          frameLayout -cll true -cl false -bv true -l "Debug Options";
            columnLayout -adj true;
              checkBox -v false -l "Pretty Output" prettyOutputCb;
            setParent ..; // columnLayout
          setParent ..; // frameLayout

    }
    else if ($action == "query")
    {
        string $option = "";

        if (`checkBox -q -v vertsCb`)
            $option += "vertices=1;";
        if (`checkBox -q -v normalsCb`)
            $option += "normals=1;";
        if (`checkBox -q -v uvsCb`)
            $option += "uvs=1;";
        if (`checkBox -q -v groupsCb`)
            $option += "groups=1;";
        if (`checkBox -q -v dedupeCb`)
            $option += "dedupe=1;";
        if (`checkBox -q -v materialsCb`)
            $option += "materials=1;";
        if (`checkBox -q -v diffuseMapsCb`)
            $option += "diffuseMaps=1;";
        if (`checkBox -q -v specularMapsCb`)
            $option += "specularMaps=1;";
        if (`checkBox -q -v bumpMapsCb`)
            $option += "bumpMaps=1;";
        if (`checkBox -q -v prettyOutputCb`)
            $option += "prettyOutput=1;";

        int $value = `intSliderGrp -q -v precisionCb`;
        $option += "precision=" + $value + ";";

        print($option);

        eval( $callback + "\"" + $option + "\"" );
    }
    return 1;
}
